buildscript {
  repositories {
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "io.spring.gradle:dependency-management-plugin:1.0.9.RELEASE"
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.16.1"
    classpath "org.springframework.boot:spring-boot-gradle-plugin:2.3.1.RELEASE"
    classpath "io.freefair.gradle:lombok-plugin:5.1.1"
  }
}

// Pull in version information from version.properties
Properties props = new Properties()
props.load(new FileInputStream("$project.rootDir/gradle/version.properties"))
props.each { prop -> if ( !project.hasProperty(prop.key) ) project.ext.set(prop.key, prop.value) }

// The pipeline can override these values by e.g. `gradle build -Prevision_version=3`
version = new Version(major: project.property('major_version'),
                      minor: project.property('minor_version'),
                      revision: project.property('revision_version'))

// The release pipeline can override this, e.g. `gradle build snapshot_suffix=`
def actual_suffix = snapshot_suffix ? "-${snapshot_suffix}" : ""
version = "${version}${actual_suffix}"
group = "${project.artifact_groupId}"

// apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: "io.freefair.lombok"


def mainClassName = 'org.njax.trinetco.netgrid.java.app.models.Main'

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        url "${artifactory_contextUrl}/repo"
        credentials {
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
    }
}

dependencies {
    runtimeOnly 'mysql:mysql-connector-java'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.0.5.RELEASE'
    implementation 'org.slf4j:slf4j-api:1.7.5'
    implementation group: 'javax.persistence', name: 'javax.persistence-api', version: '2.2'

    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: '4.0.1.Final'
    compile group: 'org.springframework', name: 'spring-orm', version: '4.0.2.RELEASE'
    compile group: 'org.springframework.security', name: 'spring-security-web', version: '4.0.2.RELEASE'

    testCompile group: 'org.springframework', name: 'spring-test', version: '4.0.5.RELEASE'
    testCompile "junit:junit:4.12"
    testCompile 'org.mockito:mockito-core:2.15.0'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            version project.version
            groupId project.group
            from components.java
        }
    }
}


artifactory {
    contextUrl = "${artifactory_contextUrl}"

    resolve {
        repository {
            repoKey = 'repo'
            maven = true
        }
    }

    publish {
        repository {
            repoKey = snapshot_suffix.toUpperCase() == "SNAPSHOT" ? snapshot_repokey:  release_repokey
            username = "${artifactory_user}"   // The publisher user name
            password = "${artifactory_password}" // The publisher password
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
}


// Workaround to allow springboot apps to publish to artifactory
jar {
    enabled = true
    manifest {
        attributes 'Main-Class': mainClassName,
                   'Implementation-Version': version
    }
}
bootJar {
  classifier = 'application'
  manifest {
      attributes 'Main-Class': mainClassName,
                 'Implementation-Title': "${group}:${rootProject.name}",
                 'Implementation-Version': version
  }
}


// Enable Reproducible Spring Boot Builds //
tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}
springBoot {
    mainClassName = mainClassName
    buildInfo {
        properties {
             time = null
        }

        doLast {
            File f = new File(destinationDir, 'build-info.properties')
            f.text = f.filterLine { !it.startsWith('#') }
        }
    }
}

class Version {
    String major, minor, revision

    String toString() {
        "$major.$minor.$revision"
    }
}
